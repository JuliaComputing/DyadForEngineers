connector MechanicalPort
    potential v::Real
    flow f::Real
end


component Spring
    parameter k::Real

    variable delta_x::Real
    variable f::Real

    flange_a = MechanicalPort()
    flange_b = MechanicalPort()
relations
    f = k * delta_x
    der(delta_x) = flange_a.v - flange_b.v
    flange_a.f = +f 
    flange_b.f = -f
end


component Damper
    parameter d::Real

    variable v::Real
    variable f::Real

    flange_a = MechanicalPort()
    flange_b = MechanicalPort()
relations
    f = d * v
    v = flange_a.v - flange_b.v
    flange_a.f = +f
    flange_b.f = -f
end


component Mass
    parameter m::Real
    parameter g::Real

    variable x::Real
    variable v::Real
    variable a::Real
    variable f::Real

    flange = MechanicalPort()
relations
    m * a = f + m * g
    der(v) = a 
    der(x) = v
    flange.f = f
    flange.v = v
end

component MassSpringDamper
    parameter mass::Real = 1000.0
    parameter gravity::Real = 0.0
    parameter stiffness::Real = 1e6
    parameter damping::Real = 1e3
    parameter initial_position::Real = 0 
 
    body = Mass(m=mass, g=gravity)
    spring = Spring(k=stiffness)
    damper = Damper(d=damping)
    port_m = MechanicalPort()
    port_sd = MechanicalPort()   
relations
    initial body.x = initial_position

    connect(port_m, damper.flange_a, spring.flange_a, body.flange)
    connect(port_sd, spring.flange_b, damper.flange_b)
end